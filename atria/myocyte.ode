
states(V=[-74.031982], O_C=0.026766, O_TC=0.012922, O_TMgC=0.190369, O_TMgMg=0.714463, F1=0.470055, F2=0.002814, O_Calse=0.431547, Ca_up=0.649195, Ca_rel=0.632613, Ca_c=1.815768, K_c=5.560224, Na_c=130.022096, Ca_d=7.1e-5, O=1.382220, Ca_i=6.5e-5, K_i=129.4859911, Na_i=8.516766, m=0.0033, h1=0.8774, h2=0.8774, d_L=1.3505e-05, f_L1= 0.9986, f_L2= 0.9986, pa=  5.3346e-05, n=  6.1319e-04, r=0.0011, s=0.9486, a_ur=3.6666e-04, i_ur=0.9987)

parameters(ist = [0.], g_K1=[3.1])

#Human atrial myocyte model with modified repolarization (hAMr), based on the model of Nygren, et al.

#UNITS:
#TIME IN SECONDS.
#VOLTAGE IN MILLIVOLTS.
#CONCENTRATIONS IN MILLIMOLES/LITER.
#CURRENT IN PICOAMPERES.
#CONDUCTANCE IN NANOSIEMENS.
#CAPACITANCE IN NANOFARADS.
#VOLUME IN NANOLITERS.
#TEMPERATURE IN KELVIN.



#Static
R=8314.0
T=306.15
F=96487.0
E_Ca_app=60.0
k_Ca=0.025
K_NaK_K=1.0
K_NaK_Na=11.0
i_CaP_max=4.0
K_CaP=0.0002
K_NaCa=0.0374842
d_NaCa=0.0003
gamma_Na=0.45
Vol_i=0.005884
Vol_c=0.000800224
Vol_d=0.00011768
tau_di=0.010
Mg_i=2.5
tau_Na=14.3
Na_b=130.0
tau_K=10.0
K_b=5.4
tau_Ca=24.7
Ca_b=1.8
Vol_up=0.0003969
Vol_rel=0.0000441
I_up_max=2800.0
k_cyca=0.0003
k_srca=0.5
k_xcs=0.4
tau_tr=0.01
alpha_rel=200000.0
r_recov=0.815
k_rel_i=0.0003
k_rel_d=0.003
pow_K_NaK_Na_15=36.4829

#Static now, may want to vary later
Cm=50
P_Na=0.0018
g_Ca_L=6.75
g_t=8.25
g_kur=2.25
g_Ks=1.0
g_Kr=0.5
#g_K1=3.1
ACh=1e-24
g_B_Na=0.060599
g_B_Ca=0.078681
i_NaK_max=68.55



pi = 1.0 /( 1.0 +exp((V+ 55.0 )/ 24.0 ))
f_Ca = Ca_d/(Ca_d+k_Ca)

 
#NERNST POTENTIALS
E_Ca = R*T/(2*F) * log(Ca_c/Ca_i)
E_K = R*T/F     * log(K_c/K_i)
E_Na = R*T/F     * log(Na_c/Na_i)

#CURRENTS     
#SODIUM CURRENT
i_Na = P_Na * m*m*m* (0.9*h1 + 0.1*h2)* Na_c * V*F*F/(R*T)  * (exp((V-E_Na)*F/(R*T))- 1.0 )/ (exp((V*F)/(R*T))- 1.0 )
h=h1*0.9+h2*0.1
m_factor = (V+25.57)/28.8
h_factor = 1/( 1.0 +exp((V+ 35.1 )/ 3.2 ))
tau_h1 = 0.03*h_factor+ 0.0003
tau_h2 = 0.12*h_factor+ 0.003
tau_m = 0.000042 *exp(-m_factor*m_factor)+ 0.000024 
m_infinity = 1.0 /( 1.0 +exp((V+ 27.12 )/-8.21))
h_infinity = 1.0 /( 1.0 +exp((V+ 63.6 )/ 5.3 ))
h1_infinity = h_infinity
h2_infinity = h_infinity

#POTASSIUM CURRENTS
#I_K1
i_K1=g_K1*pow(K_c,0.4457)*(V-E_K)/(1.0+exp(1.5*(V-E_K+3.6)*F/(R*T)))

#RAPID DELAYED RECTIFIER, i_Kr
i_Kr = g_Kr*pa*pi*(V-E_K)
pa_factor = (V+ 20.1376 )/ 22.1996
pa_infinity = 1.0 /( 1.0 +exp((V+ 15.0 )/ -6.0 ))
pi = 1.0 /( 1.0 +exp((V+ 55.0 )/ 24.0 ))
tau_pa = 0.03118 +0.21718 *exp(-pa_factor*pa_factor)

#SLOW DELAYED RECTIFIER, i_Ks
i_Ks = g_Ks*n*(V-E_K)
n_factor = (V- 20.0 )/ 20.0
n_infinity = 1.0 /( 1.0 +exp((V- 19.9 )/ -12.7 ))
tau_n = 0.7 + 0.4*exp(-n_factor*n_factor)

#RAPID TRANSIENT OUTWARD CURRENT, i_t 
i_t = g_t*r*s*(V-E_K)
r_infinity = 1.0 /( 1.0 +exp((V- 1.0 )/ -11.0 ))
s_infinity = 1.0 /( 1.0 +exp((V+ 40.5 )/ 11.5 ))
s_factor = (V+ 52.45 )/ 15.8827
tau_s = 0.025635*exp(-s_factor*s_factor)+ 0.01414
tau_r = 0.0035 *exp(-V*V/30.0/30.0)+ 0.0015

#ULTRA-RAPIDLY ACITIVATING SUSTAINED CURRENT, i_kur
i_kur = g_kur*a_ur*i_ur*(V-E_K)
a_ur_infinity = 1.0 / (1.0 + exp(-(V+6)/8.6))
i_ur_infinity = 1.0 / (1.0 + exp((V+7.5)/10.0))
tau_a_ur = 0.009 /( 1.0 +exp((V+ 5.0 )/ 12.0 ))+ 0.0005
tau_i_ur = 0.59/(1.0+exp((V+60.0)/10.0))+3.05

#Acetylcholine Potassium Current - from Kneller 2002
i_KACh = (10/(1+9.13652/(pow(ACh, 0.477811))))*(0.0517+0.4516/(1 + exp((V+59.53)/17.18)))*(V - E_K)*Cm

#CALCIUM
#L-TYPE CALCIUM CURRENT
i_Ca_L = g_Ca_L * d_L * (f_Ca*f_L1+(1.0-f_Ca)*f_L2) * (V-E_Ca_app)
d_L_factor = (V+ 35.0 )/ 30.0
d_L_infinity = 1.0 /( 1.0 +exp((V+ 9.0 )/ -5.8 ))
f_Ca = Ca_d/(Ca_d+k_Ca)
f_L_factor = V+40.0
f_L_infinity = 1.0 /( 1.0 +exp((V+ 27.4 )/ 7.1 ))
f_L1_infinity = f_L_infinity
f_L2_infinity = f_L_infinity
tau_d_L = 0.0027 *exp(-d_L_factor*d_L_factor)+ 0.002
tau_f_L1 = 0.161 *exp(-f_L_factor*f_L_factor/14.4/14.4)+ 0.010 
tau_f_L2 = 1.3323 *exp(-f_L_factor*f_L_factor/14.2/14.2)+ 0.0626

#PUMP AND EXCHANGER CURRENTS 
i_CaP = i_CaP_max*Ca_i/(Ca_i+K_CaP)
i_NaCa = K_NaCa* (Na_i*Na_i*Na_i*Ca_c*exp(F*V*gamma_Na/(R*T))-(Na_c*Na_c*Na_c*Ca_i*exp((gamma_Na-1)*V*F/(R*T))))/ (1.0+d_NaCa*(Na_c*Na_c*Na_c*Ca_i+ Na_i*Na_i*Na_i*Ca_c))
pow_Na_i_15 = pow(Na_i, 1.5)
i_NaK = i_NaK_max* K_c/(K_c+K_NaK_K)* pow_Na_i_15/(pow_Na_i_15+pow_K_NaK_Na_15)* (V+ 150.0)/(V+ 200.0)

#BACKGROUND CURRENTS 
i_B_Ca = g_B_Ca*(V-E_Ca)
i_B_Na = g_B_Na*(V-E_Na)
i_B = i_B_Na+i_B_Ca

#TOTAL CURRENT:
I = (i_Na+i_Ca_L+i_t+i_kur+i_K1+i_Kr+i_Ks+i_B+i_NaK+i_CaP+i_NaCa+i_KACh)/Cm+ist


#INTRACELLULAR CALCIUM BUFFERING
J_O_C = 200000.0*Ca_i*(1-O_C)- 476.0*O_C
J_O_TC = 78400.0*Ca_i*(1-O_TC) - 392.0*O_TC
J_O_TMgC = 200000.0*Ca_i*(1-O_TMgC-O_TMgMg) - 6.6*O_TMgC
J_O_TMgMg = 2000.0*Mg_i*(1-O_TMgC-O_TMgMg) - 666.0*O_TMgMg

#CALCIUM HANDLING BY THE SARCOPLASMIC RETICULUM
r_Ca_d_term = Ca_d/(Ca_d+k_rel_d)
r_Ca_i_term = Ca_i/(Ca_i+k_rel_i)
r_Ca_d_factor = r_Ca_d_term*r_Ca_d_term*r_Ca_d_term*r_Ca_d_term
r_Ca_i_factor = r_Ca_i_term*r_Ca_i_term*r_Ca_i_term*r_Ca_i_term
r_act = 203.8*(r_Ca_i_factor + r_Ca_d_factor)
r_inact = 33.96 + 339.6*r_Ca_i_factor
i_rel_f2 = F2/(F2+0.25)
i_rel_factor = i_rel_f2*i_rel_f2
i_rel = alpha_rel*i_rel_factor*(Ca_rel-Ca_i)
i_up = I_up_max* (Ca_i/k_cyca - k_xcs*k_xcs*Ca_up/k_srca)/ ((Ca_i+k_cyca)/k_cyca + k_xcs*(Ca_up+k_srca)/k_srca)
i_tr = (Ca_up-Ca_rel) * 2*Vol_rel*F/tau_tr
J_O_Calse = 480.0*Ca_rel*(1-O_Calse) - 400.0*O_Calse



#INTRACELLULAR SPACE AND ION CONCENTRATIONS
i_di = (Ca_d-Ca_i)*(2*Vol_d*F)/tau_di
J_O = 0.08*J_O_TC + 0.16*J_O_TMgC + 0.045*J_O_C

#TOTAL NS CHARGE INSIDE CELL -- Q in 1e-12 coulombs, NS_i is in millmoles/L -- 0.05 is Cm in nF -- good check
NS_i=(-0.05*V+(Vol_i*F*(Na_i+K_i+2*Ca_i+2*(0.08*O_TC+0.16*O_TMgC+0.045*O_C))+2*Vol_d*F*Ca_d+2*Vol_up*F*Ca_up+2*Vol_rel*F*(Ca_rel+31*O_Calse)))/(Vol_i*F)


phi_Na_en = 0 # Not specified, ASK MOLLY.
dV_dt=-I
dO_C_dt = 1e-3*J_O_C
dO_TC_dt  = 1e-3*J_O_TC
dO_TMgC_dt = 1e-3*J_O_TMgC
dO_TMgMg_dt = 1e-3*J_O_TMgMg
dF1_dt = 1e-3*(r_recov*(1.0-F1-F2) - r_act*F1)
dF2_dt = 1e-3*((r_act*F1)-(r_inact*F2))
dO_Calse_dt = 1e-3*J_O_Calse
dCa_up_dt = 1e-3*((i_up-i_tr)/(2*Vol_up*F))
dCa_rel_dt = 1e-3*((i_tr-i_rel)/(2*Vol_rel*F)-31.0*J_O_Calse)
dCa_c_dt = 1e-3*((Ca_b-Ca_c)/tau_Ca+(i_Ca_L+i_B_Ca+i_CaP-2*i_NaCa)/(2*Vol_c*F))
dK_c_dt = 1e-3*((K_b-K_c)/tau_K+(i_t+i_kur+i_K1+i_Ks+i_Kr-2*i_NaK)/(Vol_c*F))
dNa_c_dt = 1e-3*((Na_b-Na_c)/tau_Na+(i_Na+i_B_Na+3*i_NaCa+3*i_NaK+phi_Na_en)/(Vol_c*F))
dCa_d_dt = 1e-3*( -(i_Ca_L+i_di)/(2*Vol_d*F))
dO_dt = 1e-3*J_O
dCa_i_dt = 1e-3*(-((i_B_Ca+i_CaP+i_up)-(i_di+i_rel+2*i_NaCa))/(2*Vol_i*F)-J_O)
dK_i_dt =  1e-3*(-(i_t + i_kur + i_K1 + i_Ks + i_Kr - 2*i_NaK + ist*Cm)/(Vol_i*F))
dNa_i_dt = 1e-3*(-(i_Na + i_B_Na + 3*i_NaCa + 3*i_NaK + phi_Na_en)/(Vol_i*F))
dm_dt= 1e-3*(m_infinity -m )/tau_m 
dh1_dt= 1e-3*(h1_infinity -h1 )/tau_h1 
dh2_dt= 1e-3*(h2_infinity -h2 )/tau_h2 
dd_L_dt= 1e-3*(d_L_infinity -d_L )/tau_d_L 
df_L1_dt= 1e-3*(f_L1_infinity -f_L1 )/tau_f_L1 
df_L2_dt= 1e-3*(f_L2_infinity -f_L2 )/tau_f_L2 
dpa_dt= 1e-3*(pa_infinity -pa )/tau_pa 
dn_dt= 1e-3*(n_infinity -n )/tau_n 
dr_dt= 1e-3*(r_infinity -r )/tau_r 
ds_dt= 1e-3*(s_infinity -s )/tau_s 
da_ur_dt= 1e-3*(a_ur_infinity -a_ur )/tau_a_ur 
di_ur_dt= 1e-3*(i_ur_infinity -i_ur )/tau_i_ur 



